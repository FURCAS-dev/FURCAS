/**
 * Kent Modelling Framework - KMFStudio
 * Copyright (C) 2002 University of Kent at Canterbury, UK 
 * Visit www.cs.ukc.ac.uk/kmf
 *
 */

// Preliminary Declarations Section
package org.oslo.ocl20.syntax.parser;

import java.util.*;

import org.oslo.ocl20.syntax.ast.*;
import org.oslo.ocl20.syntax.ast.contexts.*;
import org.oslo.ocl20.syntax.ast.expressions.*;
import org.oslo.ocl20.syntax.ast.types.*;

import java_cup.runtime.*;
import uk.ac.kent.cs.kmf.util.*;

/**
 * 
 * @author Michael Wagner
 *
 */

//action code {:
//      Symbol sym = action_obj.CUP$Yyparse$do_action(act_num, parser, stack, top);
//	  ((SyntaxElement)sym.value).setSymbol(sym);
//	  return sym;
// :}

// Code to be put inside the parser class
parser code {:
	// Log file
	protected ILog log;
	public void setLog(ILog log) {
		this.log = log;
	}
	public ILog getLog() {
		return log;
	}

	// Factory
	protected ASTBuilder factory;
	public void setFactory(ASTBuilder factory) {
		this.factory = factory;
	}
	public ASTBuilder getFactory() {
		return factory;
	}

	// Change the method report_error to display location and the reason
	public void report_error(String message, Object info) {
		ErrorManager.reportError(log, info, message);
	}
	
	// Change the method report_error to display location and the reason
	public void report_fatal_error(String message, Object info) {
		report_error(message, info);
	}

:};

// Initialization code - before scanning
init with {:
	// Init the log
	CUP$Yyparse$actions.setLog(log);
	// Init the log
	CUP$Yyparse$actions.setFactory(factory);
:};

// Particular code for actions   
action code {:
	// Log file
	protected static ILog log;
	public static void setLog(ILog log) {
		CUP$Yyparse$actions.log = log;
	}
	public static ILog getLog() {
		return CUP$Yyparse$actions.log;
	}

	// Factory
	protected static ASTBuilder factory;
	public static void setFactory(ASTBuilder factory) {
		CUP$Yyparse$actions.factory = factory;
	}
	public static ASTBuilder getFactory() {
		return CUP$Yyparse$actions.factory;
	}

	// Check and make a variableDeclaration
	public static VariableDeclarationAS makeVariableDeclaration(OclExpressionAS nameExp, TypeAS type, OclExpressionAS init, int left, int right) {
		if (nameExp instanceof PathNameExpAS) {
			List pathName = ((PathNameExpAS)nameExp).getPathName();
			if (pathName.size() == 1) {
				String name = (String)pathName.get(0);
				return factory.buildVariableDeclaration(name, type, init);
			}
		}
		Symbol info = new Symbol(0, left, right);
		report_error("Variable declaration expected", info);
		return null;
	}
	
	// Report errors
	public static void report_error(String message, Object info) {
		ErrorManager.reportError(log, info, message);
	}

:};

// Terminals and Non Terminals Section
 
// Terminals
terminal	PACKAGE, ENDPACKAGE;
terminal	CONTEXT, INIT, DERIVE, INV, DEF, PRE, POST, BODY;

terminal	SET, BAG, SEQUENCE, COLLECTION, ORDERED_SET;
terminal	TUPLE, TUPLE_TYPE;
terminal	ITERATE;

terminal	IMPLIES;
terminal	AND, OR, XOR;
terminal	EQ, NE;
terminal	LT, GT, LE, GE;
terminal	PLUS, MINUS, UMINUS;
terminal	TIMES, DIVIDE;
terminal    INT_DIVIDE, INT_MOD;
terminal	NOT;
terminal	LET, IN;
terminal	IF, THEN, ELSE, ENDIF;

terminal	COMMA, DOT, COLON, SEMICOLON, BAR, UP, AT, QUESTION;
terminal	DOT_DOT, COLON_COLON, UP_UP;
terminal	LEFT_PAR, LEFT_BRK, LEFT_BRA, RIGHT_PAR, RIGHT_BRK, RIGHT_BRA;
terminal	MINUS_GT;

terminal	String	INTEGER, REAL, STRING, TRUE, FALSE, UNDEFINED;
terminal	String	SIMPLE_NAME;

terminal	BAD;

// ############# QVT extension ####################################################


// ############# QVT extension end ################################################
   
// Non Terminals
non terminal	PackageDeclarationAS		packageDeclaration;
non terminal	List						contextDeclList;
non terminal	ContextDeclarationAS		contextDeclaration;
non terminal	ClassifierContextDeclAS		classifierContextDecl;
non terminal	List						invOrDef;
non terminal	Object[]					defExpression;			
non terminal	PropertyContextDeclAS		propertyContextDecl;
non terminal	List						initOrDerValue;
non terminal	OperationContextDeclAS		operationContextDecl;
non terminal	OperationAS					operation;
non terminal	List						prePostOrBodyDecl;

non terminal	List						variableDeclarationList;
non terminal	VariableDeclarationAS		variableDeclaration;

non terminal	TypeAS						type;
non terminal	CollectionTypeAS			collectionType;
non terminal	CollectionKindAS			collectionKind;
non terminal	TupleTypeAS					tupleType;

non terminal	OclExpressionAS				oclExpression;
non terminal	List						argumentList;
non terminal	List						oclMessageArgumentList;
non terminal	OclMessageArgAS				oclMessageArgument;
non terminal	Boolean						isMarkedPre;

non terminal	LiteralExpAS				literalExp;
non terminal	CollectionLiteralExpAS		collectionLiteralExp;
non terminal	List						collectionLiteralParts;
non terminal	CollectionLiteralPartAS		collectionLiteralPart;
non terminal	CollectionRangeAS			collectionRange;
non terminal	TupleLiteralExpAS			tupleLiteralExp;
non terminal	PrimitiveLiteralExpAS		primitiveLiteralExp;

non	terminal	List						pathName;
non	terminal	String						simpleName;

//####################### QVT extension ##############################################################


//####################### QVT extension end ##########################################################


// Precedence and Associativity of Terminals Section
precedence left COMMA;
precedence left UP, UP_UP;
precedence left IMPLIES;
precedence left AND, OR, XOR;
precedence left EQ, NE;
precedence left LT, GT, LE, GE;
precedence left IF, THEN, ELSE, ENDIF;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE, INT_DIVIDE, INT_MOD;
precedence left NOT, UMINUS;
precedence left DOT, MINUS_GT;
precedence left AT;
precedence left LEFT_PAR, RIGHT_PAR, LEFT_BRK, RIGHT_BRK;
precedence left COLON;

// Start symbol
start with packageDeclaration;
//start with topLevel;


//############## OCL Part ###########################################################################

//
// Attaching OCL constraints at 'Model' level
//
// packageDeclaration ::= PACKAGE pathName contextDeclaration* ENDPACKAGE
//
packageDeclaration		::= PACKAGE pathName:path contextDeclList:contextDecls ENDPACKAGE
							{:
								// Create a PackageDeclaration
								RESULT = factory.buildPackageDeclaration(path, contextDecls);
							:}
							|
							contextDeclList:contextDecls
							{:
								// Create a PackageDeclaration
								RESULT = factory.buildPackageDeclaration(new Vector(), contextDecls);
							:}
							;
							
contextDeclList			::= {:
								// Create a LIST
								RESULT = new Vector();
							:}
							|
							contextDeclList:list contextDeclaration:contextDecl
							{:
								// Add element to list
								RESULT = list;
								RESULT.add(contextDecl);
							:}
							;
//
// Attaching OCL constraints at 'ModelElement' level
//
// contextDeclaration ::= propertyContextDecl | classifierContextDecl | operationContextDecl
//
contextDeclaration		::=	propertyContextDecl:contextDecl
							{:
								// Copy rule
								RESULT = contextDecl;
							:}
							|
							classifierContextDecl:contextDecl
							{:
								// Copy rule
								RESULT = contextDecl;
							:}
							|
							operationContextDecl:contextDecl
							{:
								// Copy rule
								RESULT = contextDecl;
							:}
							;
//
// 'Property' Level
//
// propertyContextDecl ::= CONTEXT pathName simpleName COLON type initOrDerValue+ 
// initOrDerValue ::= INIT COLON oclExpression | DERIVE COLON oclExpression
//
propertyContextDecl		::=	CONTEXT pathName:path simpleName:name COLON type:type initOrDerValue:constraints
							{:
								// Create PropertyContextDecl
								RESULT = factory.buildPropertyContextDeclaration(path, name, type, constraints);
							:}
							;						
initOrDerValue			::=	INIT COLON oclExpression:exp
							{:
								// Create a LIST and add constraint
								RESULT = new Vector();
								RESULT.add(factory.buildConstraint(ConstraintKindAS.INIT_LITERAL, "", exp, null));
							:}
							|
							DERIVE COLON oclExpression:exp
							{:
								// Create a LIST and add constraint
								RESULT = new Vector();
								RESULT.add(factory.buildConstraint(ConstraintKindAS.DERIVE_LITERAL, "", exp, null));
							:}
							|
							initOrDerValue:list INIT COLON oclExpression:exp
							{:
								// Add constraint to list
								RESULT = list;
								RESULT.add(factory.buildConstraint(ConstraintKindAS.INIT_LITERAL, "", exp, null));
							:}
							|
							initOrDerValue:list DERIVE COLON oclExpression:exp
							{:
								// Add constraint to list
								RESULT = list;
								RESULT.add(factory.buildConstraint(ConstraintKindAS.DERIVE_LITERAL, "", exp, null));
							:}
							;
//
// 'Classifier' level
//
// classifierContextDecl ::= CONTEXT pathName invOrDef+
// invOrDef ::= INV [simpleName] COLON oclExpression 
// invOrDef ::= DEF [simpleName] COLON defExpression 
// defExpression ::= variableDeclaration EQ oclExpression 
// defExpression ::= operation EQ oclExpression 
//
//classifierContextDecl	::=	CONTEXT:loc pathName:path invOrDef:constraints
classifierContextDecl	::=	CONTEXT:loc type:t invOrDef:constraints
							{:
								// Create a ClassifierContext
								//RESULT = factory.buildClassifierContextDeclaration(path, constraints);
								RESULT = factory.buildClassifierContextDeclaration(t, constraints);
							:}
							;
invOrDef				::=	INV simpleName:name COLON oclExpression:exp
							{:
								// Create a LIST and add constraint
								RESULT = new Vector();
								RESULT.add(factory.buildConstraint(ConstraintKindAS.INV_LITERAL, name, exp, null));
							:}
							|
							INV COLON oclExpression:exp
							{:
								// Create a LIST and add constraint
								RESULT = new Vector();
								RESULT.add(factory.buildConstraint(ConstraintKindAS.INV_LITERAL, "", exp, null));
							:}
							|
							DEF simpleName:name COLON defExpression:exp
							{:
								// Create a LIST and add constraint
								RESULT = new Vector();
								RESULT.add(factory.buildConstraint(ConstraintKindAS.DEF_LITERAL, name, (OclExpressionAS)exp[1], exp[0]));
							:}
							|
							DEF COLON defExpression:exp
							{:
								// Create a LIST and add constraint
								RESULT = new Vector();
								RESULT.add(factory.buildConstraint(ConstraintKindAS.DEF_LITERAL, "", (OclExpressionAS)exp[1], exp[0]));
							:}
							|							
							invOrDef:list INV simpleName:name COLON oclExpression:exp
							{:
								// Add constraint to list
								RESULT = list;
								RESULT.add(factory.buildConstraint(ConstraintKindAS.INV_LITERAL, name, exp, null));
							:}
							|
							invOrDef:list INV COLON oclExpression:exp
							{:
								// Add constraint to list
								RESULT = list;
								RESULT.add(factory.buildConstraint(ConstraintKindAS.INV_LITERAL, "", exp, null));
							:}
							|
							invOrDef:list DEF simpleName:name COLON defExpression:exp
							{:
								// Add constraint to list
								RESULT = list;
								RESULT.add(factory.buildConstraint(ConstraintKindAS.DEF_LITERAL, name, (OclExpressionAS)exp[1], exp[0]));
							:}
							|
							invOrDef:list DEF COLON defExpression:exp
							{:
								// Add constraint to list
								RESULT = list;
								RESULT.add(factory.buildConstraint(ConstraintKindAS.DEF_LITERAL, "", (OclExpressionAS)exp[1], exp[0]));
							:}
							;					
defExpression			::=	simpleName:name COLON type:type EQ oclExpression:exp
							{:
								// Create a container
								VariableDeclarationAS var = factory.buildVariableDeclaration(name,type,exp);
								RESULT = new Object[] {var, exp};
							:}
							|
							operation:oper EQ oclExpression:exp
							{:
								// Create a container
								RESULT = new Object[] {oper, exp};
							:}
							;

//
// 'Operation' level
//
// operationContextDecl ::= CONTEXT operation prePostOrBodyDecl+
// prePostOrBodyDecl ::= PRE [simpleName] COLON oclExpression 
// prePostOrBodyDecl ::= POST [simpleName] COLON oclExpression 
// prePostOrBodyDecl ::= BODY [simpleName] COLON oclExpression 
// operation ::= pathName LEFT_PAR [variableDeclarationList] RIGHT_PAR [COLON type] |
//               simpleName LEFT_PAR [variableDeclarationList] RIGHT_PAR [COLON type]
//
operationContextDecl	::=	CONTEXT operation:oper prePostOrBodyDecl:list
							{:
								// Create OperationContextDecl
								RESULT = factory.buildOperationContextDeclaration(oper, list);
							:}
							;							
prePostOrBodyDecl		::=	PRE simpleName:name COLON oclExpression:exp
							{:
								// Create a LIST and add constraint
								// Create a constraint
								ConstraintAS cons = factory.buildConstraint(ConstraintKindAS.PRE_LITERAL, name, exp, null);
								// Create a list
								RESULT = new Vector();
								RESULT.add(cons);
							:}
							|
							PRE COLON oclExpression:exp
							{:
								// Create a LIST and add constraint
								RESULT = new Vector();
								RESULT.add(factory.buildConstraint(ConstraintKindAS.PRE_LITERAL, "", exp, null));
							:}
							|
							POST simpleName:name COLON oclExpression:exp
							{:
								// Create a LIST and add constraint
								RESULT = new Vector();
								RESULT.add(factory.buildConstraint(ConstraintKindAS.POST_LITERAL, name, exp, null));
							:}
							|
							POST COLON oclExpression:exp
							{:
								// Create a LIST and add constraint
								RESULT = new Vector();
								RESULT.add(factory.buildConstraint(ConstraintKindAS.POST_LITERAL, "", exp, null));
							:}
							|
							BODY simpleName:name COLON oclExpression:exp
							{:
								// Create a LIST and add constraint
								RESULT = new Vector();
								RESULT.add(factory.buildConstraint(ConstraintKindAS.BODY_LITERAL, name, exp, null));
							:}
							|
							BODY COLON oclExpression:exp
							{:
								// Create a LIST and add constraint
								RESULT = new Vector();
								RESULT.add(factory.buildConstraint(ConstraintKindAS.BODY_LITERAL, "", exp, null));
							:}
							|
							prePostOrBodyDecl:list PRE simpleName:name COLON oclExpression:exp
							{:
								// Add constraint to list
								RESULT = list;
								RESULT.add(factory.buildConstraint(ConstraintKindAS.PRE_LITERAL, name, exp, null));
							:}
							|
							prePostOrBodyDecl:list PRE COLON oclExpression:exp
							{:
								// Create a constraint
								RESULT = list;
								RESULT.add(factory.buildConstraint(ConstraintKindAS.PRE_LITERAL, "", exp, null));
							:}
							|
							prePostOrBodyDecl:list POST simpleName:name COLON oclExpression:exp
							{:
								// Create a constraint
								RESULT = list;
								RESULT.add(factory.buildConstraint(ConstraintKindAS.POST_LITERAL, name, exp, null));
							:}
							|
							prePostOrBodyDecl:list POST COLON oclExpression:exp
							{:
								// Create a constraint
								RESULT = list;
								RESULT.add(factory.buildConstraint(ConstraintKindAS.POST_LITERAL, "", exp, null));
							:}
							|
							prePostOrBodyDecl:list BODY simpleName:name COLON oclExpression:exp
							{:
								// Create a constraint
								RESULT = list;
								RESULT.add(factory.buildConstraint(ConstraintKindAS.BODY_LITERAL, name, exp, null));
							:}
							|
							prePostOrBodyDecl:list BODY COLON oclExpression:exp
							{:
								// Create a constraint
								RESULT = list;
								RESULT.add(factory.buildConstraint(ConstraintKindAS.BODY_LITERAL, "", exp, null));
							:}
							;
operation				::=	pathName:path COLON_COLON simpleName:name LEFT_PAR variableDeclarationList:params RIGHT_PAR COLON type:type
							{:
								// Create an Operationn
								RESULT = factory.buildOperation(path, name, params, type);
							:}
							|
							pathName:path COLON_COLON simpleName:name LEFT_PAR variableDeclarationList:params RIGHT_PAR
							{:
								// Create an Operationn
								RESULT = factory.buildOperation(path, name, params, null);
							:}
							|
							pathName:path COLON_COLON simpleName:name LEFT_PAR RIGHT_PAR COLON type:type
							{:
								// Create an Operation
								RESULT = factory.buildOperation(path, name, new Vector(), type);
							:}
							|
							pathName:path COLON_COLON simpleName:name LEFT_PAR RIGHT_PAR
							{:
								// Create an Operationn
								RESULT = factory.buildOperation(path, name, new Vector(), null);
							:}
							|
							simpleName:name LEFT_PAR variableDeclarationList:params RIGHT_PAR COLON type:type
							{:
								// Create an Operationn
								RESULT = factory.buildOperation(new Vector(), name, params, type);
							:}
							|
							simpleName:name LEFT_PAR variableDeclarationList:params RIGHT_PAR
							{:
								// Create an Operationn
								RESULT = factory.buildOperation(new Vector(), name, params, null);
							:}
							|
							simpleName:name LEFT_PAR RIGHT_PAR COLON type:type
							{:
								// Create an Operationn
								RESULT = factory.buildOperation(new Vector(), name, new Vector(), type);
							:}
							|
							simpleName:name LEFT_PAR RIGHT_PAR
							{:
								// Create an Operationn
								RESULT = factory.buildOperation(new Vector(), name, new Vector(), null);
							:}
							;
//
// Variable declaration
//
// variableDeclarationList variableDeclaration (COMMA variableDeclaration)*
// variableDeclaration ::= simpleName [COLON type] [EQ oclExpression] 
//
variableDeclarationList ::=	variableDeclaration:var
							{:
								// Create a List
								RESULT = new Vector();
								RESULT.add(var);
							:}
							|
 							variableDeclarationList:varList COMMA variableDeclaration:var
			  				{:
			  					// Append 'var' to 'varList' 
			  					RESULT = varList;
			  					RESULT.add(var);
			  				:}
							;						
variableDeclaration	 	::= simpleName:name COLON type:type EQ oclExpression:init
							{:
								// Create a VariableDeclaration
								RESULT = factory.buildVariableDeclaration(name, type, init);
							:}
							|
							simpleName:name COLON type:type
							{:
								// Create a VariableDeclaration
								RESULT = factory.buildVariableDeclaration(name, type, null);
							:}
							|
							simpleName:name EQ oclExpression:init
							{:
								// Create a VariableDeclaration
								RESULT = factory.buildVariableDeclaration(name, null, init);
							:}
							|
							simpleName:name
							{:
								// Create a VariableDeclaration
								RESULT = factory.buildVariableDeclaration(name, null, null);
							:}
							;
//
// Type declaration
//
// type ::= pathName | collectionType | tupleType 
// collectionType ::= collectionKind LEFT_PAR type RIGHT_PAR 
// tupleType ::= TUPLE_TYPE LEFT_PAR [variableDeclaration] RIGHT_PAR
//
type					::=	pathName:path
							{:
								// Create PathNameType
								RESULT = factory.buildPathNameType(path);
							:}
							|
							collectionType:type
							{:
								// Copy rule
								RESULT = type;
							:}
							|
							tupleType:type
							{:
								// Copy rule
								RESULT = type;
							:}
							;
collectionType			::=	collectionKind:kind LEFT_PAR type:elementType RIGHT_PAR
							{:
								// Create CollectionType
								RESULT = factory.buildCollectionType(kind, elementType);
							:}
							;
tupleType 				::= TUPLE_TYPE LEFT_PAR variableDeclarationList:varList RIGHT_PAR
							{:
								// Create TupleType
								RESULT = factory.buildTupleType(varList);
							:};
							
//
// Expressions
//


//############# QVT extensions ########################################################

// ############ QVT extensions end ####################################################


oclExpression			::= 
// literalExp ::= literal
 							literalExp:exp
							{:
								// Literal expression without enumLiteralExp
								// Copy rule
								RESULT = exp;
							:}
							|
// oclExpression ::= '(' oclExpression ')'
							LEFT_PAR oclExpression:exp RIGHT_PAR
							{:
								// Copy rule
								RESULT = exp;
							:}
							|
// variableExpression ::= simpleName
// attributeCallExp ::= simpleName ['@']
// attributeCallExp ::= pathName
// associationCallExp ::= simpleName ['@']
// enumLiteralExp ::= pathName '::' simpleName
//
							pathName:path isMarkedPre:isMarkedPre
							{:
								// Create PathNameExp
								RESULT = factory.buildPathNameExp(path, isMarkedPre);
							:}
							|
// attributeCallExpression ::= oclExpression '.' simpleName ['@'] 
// associationCallExpression ::= oclExpression '.' simpleName ['@']
// iteratorExp ::= oclExpression '.' simpleName 
//
// iteratorExp ::= oclExpression '.' simpleName '(' [arguments] ')'
// operationCallExp ::= oclExpression '.' simpleName ['@'] '(' [arguments] ')'
//
							oclExpression:exp DOT simpleName:simpleName isMarkedPre:isMarkedPre
							{:
								// Create DotSelectionExp
								RESULT = factory.buildDotSelectionExp(exp, simpleName, isMarkedPre);
							:}
							|
							oclExpression:exp DOT BODY
							{:
								// Create DotSelectionExp
								RESULT = factory.buildDotSelectionExp(exp, "body", null);
							:}
							|
							oclExpression:exp DOT error
							{:
								// Error recovery
								RESULT = exp;
							:}
							|							
// operationCallExp ::= oclExpression '->' simpleName '(' [arguments] ')'
// iteratorExp ::= oclExpression '->' simpleName '(' oclExpression ')' 
//
// iteratorExp ::= oclExpression '->' simpleName '(' [varDecl ','] varDecl '|' oclExpression ')'
//
							oclExpression:exp MINUS_GT simpleName:simpleName
							{:
								// Create ArrowSelectionExp
								RESULT = factory.buildArrowSelectionExp(exp, simpleName);
							:}
							|
							oclExpression:exp MINUS_GT error
							{:
								// Error recovery
								RESULT = exp;
							:}
							|							
// operationCallExp ::= simpleName ['@'] '(' [arguments] ')' 
// operationCallExp ::= pathName '(' [arguments] ')' 
//
// iteratorExp ::= oclExpression '.' simpleName '(' [arguments] ')'
// operationCallExp ::= oclExpression '.' simpleName ['@'] '(' [arguments] ')'
//
// operationCallExp ::= oclExpression '->' simpleName '(' [arguments] ')'
// iteratorExp ::= oclExpression '->' simpleName '(' oclExpression ')' 
//
// iteratorExp ::= oclExpression '->' simpleName '(' [varDecl ','] varDecl '|' oclExpression ')'
//
							oclExpression:exp LEFT_PAR RIGHT_PAR
							{:
								// Create OperationCallExp
								RESULT = factory.buildOperationCallExp(exp, new Vector());
							:}
							|
							oclExpression:exp LEFT_PAR oclExpression:arg RIGHT_PAR
							{:
								// Create OperationCallExp
								List args = new Vector();
								args.add(arg);
								RESULT = factory.buildOperationCallExp(exp, args);
							:}
							|
							oclExpression:exp LEFT_PAR oclExpression:arg1 COMMA argumentList:list RIGHT_PAR
							{:
								// Create OperationCallExp
								List args = new Vector();
								args.add(arg1);
								args.addAll(list);
								RESULT = factory.buildOperationCallExp(exp, args);
							:}
							|
							oclExpression:exp1 LEFT_PAR oclExpression:nameExp COMMA variableDeclaration:var2 BAR oclExpression:exp2 RIGHT_PAR
							{:
								// Create first variable - check the name
								VariableDeclarationAS var1 = makeVariableDeclaration(nameExp, null, null, nameExpleft, nameExpright);
								// Create IteratorCallExp
								List varList = new Vector();
								varList.add(var1);
								varList.add(var2);
								RESULT = factory.buildIteratorCallExp(exp1, varList, exp2);
							:}
							|
							oclExpression:exp1 LEFT_PAR oclExpression:nameExp COLON type:type COMMA variableDeclaration:var2 BAR oclExpression:exp2 RIGHT_PAR
							{:
								// Create first variable - check the name
								VariableDeclarationAS var1 = makeVariableDeclaration(nameExp, type, null, nameExpleft, nameExpright);
								// Create IteratorCallExp
								List varList = new Vector();
								varList.add(var1);
								varList.add(var2);
								RESULT = factory.buildIteratorCallExp(exp1, varList, exp2);
							:}
							|
							oclExpression:exp1 LEFT_PAR variableDeclaration:var1 BAR oclExpression:exp2 RIGHT_PAR
							{:
								// Create IteratorCallExp
								List varList = new Vector();
								varList.add(var1);
								RESULT = factory.buildIteratorCallExp(exp1, varList, exp2);
							:}
					 		|
// associationCallExp ::= simpleName '[' arguments ']' ['@']
//
// iteratorExp ::= oclExpression '.' simpleName '[' arguments ']'
// associationCallExp ::= oclExpression '.' simpleName '[' arguments ']' ['@']
//
							oclExpression:exp LEFT_BRK argumentList:arguments RIGHT_BRK isMarkedPre:isMarkedPre
							{:
								// Create AssociationCallExp
								RESULT = factory.buildAssociationCallExp(exp, arguments, isMarkedPre);
							:}
							|
// iterateExp ::= oclExpression '->' 'iterate' '(' [varDecl ';' varDecl] '|' oclExpression ')'
//
							oclExpression:exp1 MINUS_GT ITERATE LEFT_PAR variableDeclaration:var1 SEMICOLON variableDeclaration:var2 BAR oclExpression:exp2 RIGHT_PAR
							{:
								// Create IterateExp
								RESULT = factory.buildIterateExp(exp1, var1, var2, exp2);
							:}
							|
							oclExpression:exp1 MINUS_GT ITERATE LEFT_PAR variableDeclaration:var2 BAR oclExpression:exp2 RIGHT_PAR
							{:
								// Create IterateExp
								RESULT = factory.buildIterateExp(exp1, null, var2, exp2);
							:}
							|
// Unary expression
// oclExpression ::= NOT postfixExp | MINUS postfixExp
//
							NOT oclExpression:opd
							{:
								// Create NotExp
								RESULT = factory.buildNotExp(opd);
							:}
							|
							MINUS oclExpression:opd
							{:
								// Create an OperationCallExp
								RESULT = factory.buildOperationCallExp("-", opd, null);
							:}
							%prec UMINUS
							|
// 
// Binary expressions
// multiplicativeExp ::= unaryExp ((TIMES | DIVIDE) unaryExp)*
// additiveExp :: multiplicativeExp ((PLUS | MINUS) additiveExp)*
// ifExp ::= additiveExp | IF oclExpression THEN oclExpression ELSE oclExpression ENDIF 
// relationalExp ::= ifExp ((GT | GE | LT | LE) ifExp)*
// equalityExp ::= relationalExp ((EQ | NE))* relationalExp)*
// logicalExp :: equalityExp ((AND | OR | XOR) equalityExp)*       
// impliesExp :: logicalExp (IMPLIES logicalExp)*
//
							oclExpression:left TIMES oclExpression:right
							{:
								// Create an OperationCallExp
								RESULT = factory.buildOperationCallExp("*", left, right);
							:}
							|
							oclExpression:left DIVIDE oclExpression:right
							{:
								// Create an OperationCallExp
								RESULT = factory.buildOperationCallExp("/", left, right);
							:}
							|
							oclExpression:left INT_DIVIDE oclExpression:right
							{:
								// Create an OperationCallExp
								RESULT = factory.buildOperationCallExp("div", left, right);
							:}
							|
							oclExpression:left INT_MOD oclExpression:right
							{:
								// Create an OperationCallExp
								RESULT = factory.buildOperationCallExp("mod", left, right);
							:}
							|
					
							/* Additive expression */
							oclExpression:left PLUS oclExpression:right
							{:
								// Create an OperationCallExp
								RESULT = factory.buildOperationCallExp("+", left, right);
							:}
							|
							oclExpression:left MINUS oclExpression:right
							{:
								// Create an OperationCallExp
								RESULT = factory.buildOperationCallExp("-", left, right);
							:}
							|

							/* If expression */
							IF oclExpression:condition THEN oclExpression:thenExp ELSE oclExpression:elseExp ENDIF
							{:
								// Create IfExp
								RESULT = factory.buildIfExp(condition, thenExp, elseExp);
							:}
							|
							
							/* Relation expression */
							oclExpression:left LT oclExpression:right
							{:
								// Create an OperationCallExp
								RESULT = factory.buildOperationCallExp("<", left, right);
							:}
							|
							oclExpression:left GT oclExpression:right
							{:
								// Create an OperationCallExp
								RESULT = factory.buildOperationCallExp(">", left, right);
							:}
							|
							oclExpression:left LE oclExpression:right
							{:
								// Create an OperationCallExp
								RESULT = factory.buildOperationCallExp("<=", left, right);
							:}
							|
							oclExpression:left GE oclExpression:right
							{:
								// Create an OperationCallExp
								RESULT = factory.buildOperationCallExp(">=", left, right);
							:}
							|
					
							/* Equality expression */
							oclExpression:left EQ oclExpression:right
							{:
								// Create an OperationCallExp
								RESULT = factory.buildOperationCallExp("=", left, right);
							:}
							|
							oclExpression:left NE oclExpression:right
							{:
								// Create an OperationCallExp
								RESULT = factory.buildOperationCallExp("<>", left, right);
							:}
							|
					
							/* Logical expression */
							oclExpression:left AND oclExpression:right
							{:
								// Create AndExp
								RESULT = factory.buildAndExp(left, right);
							:}
							|
							oclExpression:left OR oclExpression:right
							{:
								// Create OrExp
								RESULT = factory.buildOrExp(left, right);
							:}
							|
							oclExpression:left XOR oclExpression:right
							{:
								// Create OrExp
								RESULT = factory.buildXorExp(left, right);
							:}
							|
										
							/* Implication expression */
							oclExpression:left IMPLIES oclExpression:right
							{:
								// Create ImpliesExp
								RESULT = factory.buildImpliesExp(left, right);
							:}
							|
// 
// Special expressions
// 
// letExp ::= impliesExp | LET variableDeclaration+ IN oclExpression
// oclMessageExp ::= oclExpression UP_UP simpleName LEFT_PAR [oclMessageArgumentList] RIGHT_PAR 
// oclMessageExp ::= oclExpression UP simpleName LEFT_PAR [oclMessageArgumentList] RIGHT_PAR
//
							/* Let expression */
							LET variableDeclarationList:variables IN oclExpression:exp
							{:
								// Create LetExp
								RESULT = factory.buildLetExp(variables, exp);
							:}
							|

							/* OCL message expression */
							oclExpression:target UP_UP simpleName:name LEFT_PAR oclMessageArgumentList:arguments RIGHT_PAR
							{:
								// Create OclMessageExpAS
								RESULT = factory.buildOclMessageExp(OclMessageKindAS.UP_UP_LITERAL, target, name, arguments);
							:}
				  			|
				  			oclExpression:target UP_UP simpleName:name LEFT_PAR RIGHT_PAR
							{:
								// Create OclMessageExpAS
								RESULT = factory.buildOclMessageExp(OclMessageKindAS.UP_UP_LITERAL, target, name, new Vector());
							:}
				  			|
				  			oclExpression:target UP simpleName:name LEFT_PAR oclMessageArgumentList:arguments RIGHT_PAR
							{:
								// Create OclMessageExp
								RESULT = factory.buildOclMessageExp(OclMessageKindAS.UP_LITERAL, target, name, arguments);
							:}
				  			|
				  			oclExpression:target UP simpleName:name LEFT_PAR RIGHT_PAR
							{:
								// Create OclMessageExp
								RESULT = factory.buildOclMessageExp(OclMessageKindAS.UP_LITERAL, target, name, new Vector());
							:}
               				;


// Argments
// argumentList ::= oclExpression (COMMA oclExpression)*
// oclMessageArgumentList ::= oclMessageArgument (COMMA oclMessageArgument)*
// oclMessageArgument ::= QUESTION [COLON types] | oclExpression 				
argumentList 			::= oclExpression:arg
							{:
								// Create a List
								List seq = new Vector();
								seq.add(arg);
								RESULT = seq;
							:}
			  				|
			  				argumentList:argList COMMA oclExpression:arg
			  				{:
			  					// Append 'arg' to 'argList' 
			  					RESULT = argList;
			  					argList.add(arg);
			  				:}
							;
oclMessageArgumentList	::=	oclMessageArgument:arg
							{:
								// Create List
								List seq = new Vector();
								seq.add(arg);
								RESULT = seq;
							:}							
						  	|
							oclMessageArgumentList:argList COMMA oclMessageArgument:arg
							{:								
			  					// Append 'arg' to 'argList' 
			  					RESULT = argList;
			  					argList.add(arg);
							:}
							;
oclMessageArgument 		::=	QUESTION
							{:
								// Create OclMessageArg
								OclMessageArgAS arg = factory.buildOclMessageArg(null,null);
								RESULT = arg;
							:}
				  			|
				  			QUESTION COLON type:type
							{:
								// Create OclMessageArg
								OclMessageArgAS arg = factory.buildOclMessageArg(null,type);
								RESULT = arg;
							:}
				  			|
				  			oclExpression:exp
							{:
								// Create OclMessageArg
								RESULT = factory.buildOclMessageArg(exp,null);
							:}
				  			;

isMarkedPre				::= 
							{:
								RESULT = new Boolean(false);
							:}
							|
							AT PRE
							{:
								RESULT = new Boolean(true);
							:}
							;
					

//
// Literal expressions
//
// literalExp ::= enumLiteralExp 
// literalExp ::= collectionLiteralExp 
// literalExp ::= tupleLiteralExp 
// literalExp ::= primitiveLiteralExp 
// enumLiteralExp ::= pathName - or an 
// collectionLiteralExp ::= collectionKind LEFT_BRA collectionLiteralParts RIGHT_BRA 
// collectionLiteralExp ::= collectionKind LEFT_BRA RIGHT_BRA 
// collectionKind ::= SET 
// collectionKind ::= BAG 
// collectionKind ::= SEQUENCE 
// collectionKind ::= COLLECTION 
// collectionLiteralParts ::= collectionLiteralPart 
// collectionLiteralParts ::= collectionLiteralParts COMMA collectionLiteralPart 
// collectionLiteralPart ::= oclExpression 
// collectionLiteralPart ::= collectionRange 
// collectionRange ::= oclExpression DOT_DOT oclExpression 
// tupleLiteralExp ::= TUPLE LEFT_BRA variableDeclarationList RIGHT_BRA 
// primitiveLiteralExp ::= INTEGER 
// primitiveLiteralExp ::= REAL 
// primitiveLiteralExp ::= STRING 
// primitiveLiteralExp ::= TRUE 
// primitiveLiteralExp ::= FALSE
//
literalExp				::=	collectionLiteralExp:exp
							{:
								RESULT = exp;
							:}
							|
							tupleLiteralExp:exp
							{:
								RESULT = exp;
							:}
							|
							primitiveLiteralExp:exp
							{:
								RESULT = exp;
							:}
//------------------------ Extension ------------------------
							|
							collectionType:typeExpr
							{:
								RESULT = typeExpr;
							:}
							|
							tupleType:typeExpr 
							{:
								RESULT = typeExpr;
							:}
//------------------------ Extension ------------------------
							;						
collectionLiteralExp	::= collectionKind:kind LEFT_BRA collectionLiteralParts:parts RIGHT_BRA
							{:
								// Create CollectionLiteralExp
								RESULT = factory.buildCollectionLiteralExp(kind, parts);
							:}
							|
							collectionKind:kind LEFT_BRA RIGHT_BRA
							{:
								// Create CollectionLiteralExp
								RESULT = factory.buildCollectionLiteralExp(kind, new Vector());
							:}
							;
collectionKind			::=	SET
							{:
								// Set kind to SET
								RESULT = CollectionKindAS.SET_LITERAL;
							:}
							|
							BAG
							{:
								// Set kind to BAG
								RESULT = CollectionKindAS.BAG_LITERAL;
							:}
							|
							SEQUENCE
							{:
								// Set kind to SEQUENCE
								RESULT = CollectionKindAS.SEQUENCE_LITERAL;
							:}
							|
							COLLECTION
							{:
								// Set kind to COLLECTION
								RESULT = CollectionKindAS.COLLECTION_LITERAL;
							:}
							|
							ORDERED_SET
							{:
								// Set kind to ORDERED_SET
								RESULT = CollectionKindAS.ORDERED_SET_LITERAL;
							:}
							;
collectionLiteralParts	::=	collectionLiteralPart:colPart
							{:
								// Create a List
								List seq = new Vector();
								seq.add(colPart);
								RESULT = seq;
							:}
							|
							collectionLiteralParts:seq COMMA collectionLiteralPart:colPart
							{:
								// Add collPart to seq
								RESULT = seq;
								seq.add(colPart);
							:}
							;
collectionLiteralPart	::=	oclExpression:exp
							{:
								// Create CollectionItem
								RESULT = factory.buildCollectionItem(exp);
							:}
							|
							collectionRange:range
							{:
								// Copy rule
								RESULT = range;
							:}
							;
collectionRange			::= oclExpression:first DOT_DOT oclExpression:last
							{:
								// Create CollectionRange
								RESULT = factory.buildCollectionRange(first, last);
							:}
							;
tupleLiteralExp 		::= TUPLE LEFT_BRA variableDeclarationList:seq RIGHT_BRA
							{:
								// Create TupleLiteralExp
								RESULT = factory.buildTupleLiteralExp(seq);
							:}
 							;
primitiveLiteralExp		::=	INTEGER:value
							{:
								// Create IntegerLiteralExp
								RESULT = factory.buildIntegerLiteralExp(value);
							:}
							|
							REAL:value
							{:
								// Create RealLiteralExp
								RESULT = factory.buildRealLiteralExp(value);
							:}
							|
							STRING:value
							{:
								// Create StringLiteralExp
								RESULT = factory.buildStringLiteralExp(value);
							:}
							|
							TRUE:value
							{:
								// Create BooleanLiteralExp
								RESULT = factory.buildBooleanLiteralExp(value);
							:}
							|
							FALSE:value
							{:
								// Create BooleanLiteralExp
								RESULT = factory.buildBooleanLiteralExp(value);
							:}
							|
							UNDEFINED:value
							{:
								// Create BooleanLiteralExp
								RESULT = factory.buildBooleanLiteralExp(value);
							:}
							;

//
// Names
//
// pathName ::= simpleName (COLON_COLON simpleName)*
// simpleName ::= SIMPLE_NAME
//
pathName				::=	simpleName:name
							{:
								// Create a
								List seq = new Vector();
								seq.add(name);
								RESULT = seq;
							:}
							|
							pathName:path COLON_COLON simpleName:name
							{:
								// Add name to path
								RESULT = path;
								path.add(name);
							:}
							;
simpleName				::=	SIMPLE_NAME:value
							{:
								RESULT = value;
							:}
							;
						
