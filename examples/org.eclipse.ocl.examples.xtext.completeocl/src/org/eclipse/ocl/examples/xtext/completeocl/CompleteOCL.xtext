/**
 * <copyright>
 *
 * Copyright (c) 2010 E.D.Willink and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     E.D.Willink - initial API and implementation
 *
 * </copyright>
 *
 * $Id: CompleteOCL.xtext,v 1.8 2011/02/08 17:53:05 ewillink Exp $
 */
grammar org.eclipse.ocl.examples.xtext.completeocl.CompleteOCL
with org.eclipse.ocl.examples.xtext.essentialocl.EssentialOCL

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "platform:/resource/org.eclipse.ocl.examples.xtext.base/model/BaseCST.ecore" as base
import "platform:/resource/org.eclipse.ocl.examples.xtext.essentialocl/model/EssentialOCLCST.ecore" as essentialocl
import "platform:/resource/org.eclipse.ocl.examples.xtext.oclinecore/model/OCLinEcoreCST.ecore" as oclinecore
import "platform:/resource/org.eclipse.ocl.examples.pivot/model/pivot.ecore" as pivot
import "platform:/resource/org.eclipse.ocl.examples.xtext.completeocl/model/CompleteOCLCST.ecore"
//generate completeOCL "http://www.eclipse.org/ocl/examples/xtext/completeocl/CompleteOCL"
CompleteOCLDocumentCS returns CompleteOCLDocumentCS:
	(ownedImport+=ImportCS)*
	(packages+=PackageDeclarationCS | contexts+=ContextDeclCS)*;

NavigationOperator:						// Intended to be overridden
	EssentialOCLNavigationOperator | '^' | '^^'
;

UnrestrictedName returns ecore::EString:
	EssentialOCLUnrestrictedName
	| 'body'
	| 'context'
	| 'def'
	| 'derive'
	//|	'endpackage'
	| 'import'
	| 'init'
	| 'inv'
	//|	'package'
	|	'post'
	|	'pre'
	//|	'static'
;

PrimitiveTypeIdentifier:
	'Boolean'
	| 'Integer'
	| 'Real'
	| 'String'
	| 'UnlimitedNatural'
	| 'OclAny'
	| 'OclInvalid'
	| 'OclMessage'
	| 'OclState'
	| 'OclVoid';

BodyCS returns BodyCS:
	'body' (name=UnrestrictedName)? ':' ownedExpression=ExpCS;

ClassifierContextDeclCS returns ClassifierContextDeclCS:
	'context' (selfName=UnrestrictedName ':')?
	(namespace+=[pivot::Namespace|Identifier] '::')*
	classifier=[pivot::Type|Identifier]
	(rules+=InvCS | rules+=DefCS)*;

ContextDeclCS returns ContextDeclCS:
	PropertyContextDeclCS
	| ClassifierContextDeclCS
	| OperationContextDeclCS;

DefCS returns DefCS:
	(static?='static')? stereotype='def' (name=UnrestrictedName)? ':' constrainedName=UnrestrictedName
	('(' (parameters+=ParameterCS (',' parameters+=ParameterCS)*)? ')')?
	':' (ownedType=TypeExpCS)? '=' ownedExpression=ExpCS;

DerCS returns DerCS:
	stereotype='derive' ':' ownedExpression=ExpCS;

ImportCS returns base::ImportCS:
	'import' (name=Identifier ':')? namespace=[pivot::Namespace|SINGLE_QUOTED_STRING];

InitCS returns InitCS:
	stereotype='init' ':' ownedExpression=ExpCS;

InvCS returns InvCS:
	stereotype='inv' (name=UnrestrictedName ('(' ownedReason=ExpCS ')')?)? ':' ownedExpression=ExpCS;

OperationContextDeclCS returns OperationContextDeclCS:
	'context' (namespace+=[pivot::Namespace|Identifier] '::')*
	operation=[pivot::Operation|Identifier] '(' 
	(parameters+=ParameterCS (',' parameters+=ParameterCS)*)?
	')' ':' (ownedType=TypeExpCS)?
	((rules+=PreCS) | (rules+=PostCS) | (rules+=BodyCS))*;

PackageDeclarationCS returns PackageDeclarationCS:
	'package' (namespace+=[pivot::Namespace|Identifier] '::')*
	package=[pivot::Package|Identifier]
	(contexts+=ContextDeclCS)* 'endpackage';

ParameterCS returns essentialocl::VariableCS:
	(name=UnrestrictedName ':')? ownedType=TypeExpCS;

PostCS returns PostCS:
	stereotype='post' (name=UnrestrictedName)? ':' ownedExpression=ExpCS;

PreCS returns PreCS:
	stereotype='pre' (name=UnrestrictedName)? ':' ownedExpression=ExpCS;

PropertyContextDeclCS returns PropertyContextDeclCS:
	'context' (namespace+=[pivot::Namespace|Identifier] '::')*
	property=[pivot::Property|Identifier] ':' ownedType=TypeExpCS
	((rules+=InitCS (rules+=DerCS)?)? | (rules+=DerCS (rules+=InitCS)?));

//---------------------------------------------------------------------------------
//	EssentialOCL overrides
//---------------------------------------------------------------------------------
NavigatingExpCS_Base returns essentialocl::NamedExpCS:
	IndexExpCS
	({PreExpCS.namedExp=current} '@' 'pre')?
;

NavigatingArgExpCS returns essentialocl::ExpCS:
	({OclMessageArgCS} '?')
	| ExpCS
;
