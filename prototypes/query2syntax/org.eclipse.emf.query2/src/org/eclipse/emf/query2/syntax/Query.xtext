grammar org.eclipse.emf.query2.syntax.Query hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate query "http://www.eclipse.org/emf/query2/Query"

Model:
	(imports+=Import)*
	(defaultQuery=MQLquery)?
	(namedQueries+=NamedQuery)*
;

Import :
	'import' impURI=STRING;

NamedQuery:
	name=ID ":" query=MQLquery
;

MQLquery :
	"from" fromEntries+=FromEntry ("," fromEntries+=FromEntry)* 
	"select" selectEntries+=SelectEntry ("," selectEntries+=SelectEntry)*
	("where" whereEntry=WhereEntry)?
;

SelectEntry:
	select=[FromEntry] ("." attribute=[ecore::EAttribute])?
; 

FromEntry:
	type=[ecore::EClass] (withoutsubtypes?="withoutsubtypes" ("{"withoutsubtypesTypes+=[ecore::EClass] ("," withoutsubtypesTypes+=[ecore::EClass])* "}")?)? "as" alias=ID (scopeClause=ScopeClause)?
;

ScopeClause:
	ResourceScope | ElementScope
;

ResourceScope:
	(notIn?="not")? "in" "resources" "{" uris+=STRING ("," uris+=STRING)* "}"
;

ElementScope:
	(notIn?="not")? "in" "elements" "{" uris+=STRING ("," uris+=STRING)* "}"
;

WhereEntry returns WhereEntry:
	AndWhereEntry ({OrWhereEntry.entries+=current} 
    	("or" entries+=AndWhereEntry)+)?
;
 
AndWhereEntry returns WhereEntry:
	ConcreteWhereEntry ({AndWhereEntry.entries+=current} 
    	("and" entries+=ConcreteWhereEntry)+)?

;

ConcreteWhereEntry returns WhereEntry:
	ParWhereEntry | ExpressionWhereEntry
;

ParWhereEntry  returns WhereEntry:
	"(" WhereEntry ")"
;

ExpressionWhereEntry:
	lhs=AliasAttributeExpression operator=Operator rhs=Expression 
;

AliasAttributeExpression:
	alias=[FromEntry] ("." attribute=[ecore::EStructuralFeature])?
;

Expression:
	AliasAttributeExpression | DoubleExpression | LongExpression | StringExpression | NullExpression | BooleanExpression | QueryExpression
;

DoubleExpression: 
	value=SIGNED_DOUBLE
; 

LongExpression: 
	value=SINGED_LONG	
; 

StringExpression: 
	value=STRING
; 

NullExpression:
	value="null"
; 

BooleanExpression: 
	(true?="true" | "false")
;

QueryExpression:
	"("value=MQLquery")" 
;

enum Operator:
	lessThen="<" | greaterThen=">" | lessEqual="<=" | greaterEqual=">=" | equal="=" | notEqual="!=" | like="like" | notLike="not like" | notIn="not in" | in="in"
;

terminal SINGED_LONG returns ecore::ELong: '-'?('0'..'9')+;	
terminal SIGNED_DOUBLE returns ecore::EDouble: '-'?('0'..'9')+ ('.' ('0'..'9')+)?;

terminal ID  		: '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
terminal STRING	: 
			'"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\') | !('\\'|'"') )* '"' |
			"'" ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\') | !('\\'|"'") )* "'"
		; 
terminal ML_COMMENT	: '/*' -> '*/';
terminal SL_COMMENT 	: '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS			: (' '|'\t'|'\r'|'\n')+;